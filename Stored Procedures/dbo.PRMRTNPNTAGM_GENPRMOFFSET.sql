SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[PRMRTNPNTAGM_GENPRMOFFSET]
(
 @PINUM VARCHAR(14), --返点协议号
 @PIOPERGID INT, --PRMOFFSET.FILLER 是整型
 @PIBTYPE SMALLINT, --生成方式 0手工 1自动 2其他单据
 @POPMROFFSETNUM VARCHAR(14) OUTPUT,
 @POMSG VARCHAR(255) OUTPUT
)
AS
BEGIN
  DECLARE
    @VENDOR INT,
    @VBEGINTIME DATETIME,
    @VENDTIME DATETIME,
    @VSTOREGID INT,
    @VGDGID INT,
    @VTOTAL MONEY, --销售总金额，不能作他用
    @VRATE MONEY, --返点比例
    @VRETURNTOTAL MONEY, --返点总额
    @VQTY MONEY,
    @VDTLTOTAL MONEY, --明细销售总额
    @VDIFFTOTAL MONEY, --明细和汇总的金额之差
    @VRET SMALLINT,
    @VSETTLENO INT,
    @VMAXLINE INT,
    @VMAXITEM INT
  SELECT @VENDOR = VENDOR, @VBEGINTIME = BEGINTIME, @VENDTIME = ENDTIME
  FROM PRMRTNPNTAGM(NOLOCK)
  WHERE NUM = @PINUM
  SELECT @VSETTLENO = MAX(NO) FROM MONTHSETTLE(NOLOCK)
  --销售总额
  SELECT @VTOTAL = ISNULL(SUM(DT1 + DT2 - DT5 - DT6), 0)
  FROM OUTDRPT O(NOLOCK), GOODS G(NOLOCK), PRMRTNPNTAGMDTL D(NOLOCK), PRMRTNPNTAGMLACSTORE S(NOLOCK)
  WHERE ADATE >= @VBEGINTIME AND ADATE <= @VENDTIME
    AND O.BGDGID = G.GID
    AND G.BILLTO = @VENDOR
    AND D.NUM = @PINUM
    AND S.NUM = @PINUM
    AND SUBSTRING(G.F1, 1, LEN(D.DEPT)) = D.DEPT
    AND G.BRAND = ISNULL(D.BRAND, G.BRAND)
    AND NOT EXISTS(SELECT NUM FROM PRMRTNPNTAGMEXGDDTL E(NOLOCK) WHERE E.NUM = @PINUM AND E.GDGID = G.GID)
    AND O.ASTORE = S.STOREGID
  --销售总额为0即可返回
  IF @VTOTAL = 0
  BEGIN
    SET @POMSG = '销售额为0'
    RETURN(1)
  END
  --返点总额
  SELECT @VRATE = RATE
  FROM PRMRTNPNTAGMRATEDTL(NOLOCK)
  WHERE NUM = @PINUM AND LOWAMT < @VTOTAL AND @VTOTAL <= HIGHAMT
  IF @VRATE IS NULL SET @VRATE = 0
  SELECT @VRETURNTOTAL = @VTOTAL * @VRATE / 100
  IF @VRETURNTOTAL = 0
  BEGIN
    SET @POMSG = '返点额为0'
    RETURN(1)
  END
  --计算各门店返点额，插入促销补差单明细明细临时表
  DELETE FROM PRMOFFSETDTLDTLTEMP WHERE SPID = @@SPID --删除临时表数据
  DECLARE C_OUTDRPT CURSOR FOR
    SELECT ASTORE, BGDGID, ISNULL(SUM(DQ1 + DQ2 - DQ5 - DQ6), 0) QTY, ISNULL(SUM(DT1 + DT2 - DT5 - DT6), 0) TOTAL
    FROM OUTDRPT O(NOLOCK), GOODS G(NOLOCK), PRMRTNPNTAGMDTL D(NOLOCK), PRMRTNPNTAGMLACSTORE S(NOLOCK)
    WHERE ADATE >= @VBEGINTIME AND ADATE <= @VENDTIME
      AND O.BGDGID = G.GID
      AND G.BILLTO = @VENDOR
      AND D.NUM = @PINUM
      AND S.NUM = @PINUM
      AND SUBSTRING(G.F1, 1, LEN(D.DEPT)) = D.DEPT
      AND G.BRAND = ISNULL(D.BRAND, G.BRAND)
      AND NOT EXISTS(SELECT NUM FROM PRMRTNPNTAGMEXGDDTL E(NOLOCK) WHERE E.NUM = @PINUM AND E.GDGID = G.GID)
      AND O.ASTORE = S.STOREGID
    GROUP BY ASTORE, BGDGID
  OPEN C_OUTDRPT
  FETCH NEXT FROM C_OUTDRPT INTO @VSTOREGID, @VGDGID, @VQTY, @VDTLTOTAL
  WHILE @@FETCH_STATUS = 0
  BEGIN
    --插入临时表的金额是销售额，插入完毕后统一转换成返点额
    EXEC ADDONEPRMOFFSETDTLDTLTEMPRECORD @PINUM, @PINUM, 1, 'PRMRTNPNTAGM', @VSTOREGID, @VGDGID, @VQTY, @VDTLTOTAL
    FETCH NEXT FROM C_OUTDRPT INTO @VSTOREGID, @VGDGID, @VQTY, @VDTLTOTAL
  END
  CLOSE C_OUTDRPT
  DEALLOCATE C_OUTDRPT
  --转换成返点额，会造成尾差
  UPDATE PRMOFFSETDTLDTLTEMP SET SAMT = SAMT * @VRETURNTOTAL / @VTOTAL, RAMT = RAMT * @VRETURNTOTAL / @VTOTAL
  --计算尾差
  SELECT @VDTLTOTAL = ISNULL(SUM(SAMT), 0)
  FROM PRMOFFSETDTLDTLTEMP(NOLOCK)
  WHERE SPID = @@SPID AND NUM = @PINUM
  IF @VDTLTOTAL <> @VRETURNTOTAL --存在尾差
  BEGIN
    SET @VDIFFTOTAL = @VRETURNTOTAL - @VDTLTOTAL
    SELECT TOP 1 @VMAXLINE = LINE, @VMAXITEM = ITEM
    FROM PRMOFFSETDTLDTLTEMP(NOLOCK)
    WHERE SPID = @@SPID AND NUM = @PINUM
    ORDER BY LINE, ITEM DESC
    UPDATE PRMOFFSETDTLDTLTEMP
    SET SAMT = SAMT + @VDIFFTOTAL, RAMT = RAMT + @VDIFFTOTAL
    WHERE SPID = @@SPID AND NUM = @PINUM
      AND LINE = @VMAXLINE
      AND ITEM = @VMAXITEM
  END
  --计算各商品返点额，插入促销补差单明细临时表
  DELETE FROM PRMOFFSETDTLTEMP WHERE SPID = @@SPID --删除临时表数据
  DECLARE C_PRMOFFSETDTLDTLTEMP CURSOR FOR
    SELECT GDGID, ISNULL(SUM(SQTY), 0) SQTY, ISNULL(SUM(SAMT), 0) SAMT
    FROM PRMOFFSETDTLDTLTEMP(NOLOCK)
    WHERE SPID = @@SPID AND NUM = @PINUM
    GROUP BY GDGID
  OPEN C_PRMOFFSETDTLDTLTEMP
  FETCH NEXT FROM C_PRMOFFSETDTLDTLTEMP INTO @VGDGID, @VQTY, @VDTLTOTAL
  WHILE @@FETCH_STATUS = 0
  BEGIN
    EXEC ADDONEPRMOFFSETDTLTEMPRECORD @PINUM, @VSETTLENO, @VGDGID, @PINUM, 1, 'PRMRTNPNTAGM', @VQTY, @VDTLTOTAL, @VBEGINTIME, @VENDTIME
    FETCH NEXT FROM C_PRMOFFSETDTLDTLTEMP INTO @VGDGID, @VQTY, @VDTLTOTAL
  END
  CLOSE C_PRMOFFSETDTLDTLTEMP
  DEALLOCATE C_PRMOFFSETDTLDTLTEMP
  --计算补差进价和补差差价
  UPDATE PRMOFFSETDTLTEMP
  SET DIFFPRC = TOTAL / QTY
  WHERE SPID = @@SPID AND NUM = @PINUM
  --插入正式表
  DECLARE
    @VOFFSETTYPE SMALLINT,
    @VOFFSETCALCTYPE SMALLINT,
    @VGATHERINGMODE SMALLINT,
    @VPSRCODE VARCHAR,
    @VPSRGID INT,
    @VSETTLEDEPTCODE VARCHAR(10)
  EXEC OPTREADINT 727, 'OFFSETTYPE', 0, @VOFFSETTYPE OUTPUT
  SET @VOFFSETCALCTYPE = 1 --差价模式
  EXEC OPTREADINT 727, 'GATHERINGMODE', 0, @VGATHERINGMODE OUTPUT
  EXEC OPTREADSTR 727, 'PSRCODE', '-', @VPSRCODE OUTPUT
  EXEC OPTREADSTR 727, 'SETTLEDEPTCODE', '', @VSETTLEDEPTCODE OUTPUT
  SELECT @VPSRGID = GID FROM EMPLOYEE(NOLOCK) WHERE CODE = @VPSRCODE
  IF @VPSRGID IS NULL SET @VPSRGID = 1
  EXEC @VRET = GETFROMPRMOFFSETTEMP @PINUM, @VENDOR, @VSETTLENO, @PIOPERGID,
    @VOFFSETTYPE, @VOFFSETCALCTYPE, @VGATHERINGMODE, @VPSRGID,
    @VSETTLEDEPTCODE, -1, @PIBTYPE, '促销返点协议',
    @PINUM, @POPMROFFSETNUM OUTPUT, @POMSG OUTPUT
  IF @VRET <> 0 RETURN(@VRET)
  --固化生效单位
  INSERT INTO PRMOFFSETLACDTL(NUM, STOREGID)
  SELECT @POPMROFFSETNUM, STOREGID
  FROM PRMRTNPNTAGMLACSTORE(NOLOCK)
  WHERE NUM = @PINUM
  --删除临时表数据
  DELETE FROM PRMOFFSETDTLTEMP WHERE SPID = @@SPID
  DELETE FROM PRMOFFSETDTLDTLTEMP WHERE SPID = @@SPID

  RETURN(0)
END
GO
