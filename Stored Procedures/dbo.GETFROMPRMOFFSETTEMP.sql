SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[GETFROMPRMOFFSETTEMP]
(
 @PINUM VARCHAR(14), --临时表的单号
 @PIVENDOR INT,
 @PISETTLENO INT,
 @PIOPERGID INT,
 @PIOFFSETTYPE SMALLINT, --补差方式
 @PIOFFSETCALCTYPE SMALLINT, --0进价模式 1差价模式
 @PIGATHERINGMODE SMALLINT, --收款方式 0非账扣 1账扣
 @PIPSR INT, --采购员
 @PISETTLEDEPT VARCHAR(10), --费用结算组
 @PIPAYDIRECT SMALLINT, --付款方向 -1为付款，1为收款
 @PIBTYPE SMALLINT, --生成方式 0为人工生成 1为自动生成
 @PIGENCLS VARCHAR(20), --由什么单据（母单据）生成
 @PIGENNUM VARCHAR(14), --母单据的单号
 @PONUM VARCHAR(14) OUTPUT, --单号
 @POMSG VARCHAR(255) OUTPUT
)
AS
BEGIN
  DECLARE
    @VNUM VARCHAR(14),
    @VTOTAL MONEY,
    @VAMOUNT MONEY,
    @VTAX MONEY,
    @VNOTE VARCHAR(255),
    @VRECCNT INT,
    @VSRC INT
  IF NOT EXISTS(SELECT * FROM PRMOFFSETDTLDTLTEMP(NOLOCK) WHERE SPID = @@SPID AND NUM = @PINUM)
    OR NOT EXISTS(SELECT * FROM PRMOFFSETDTLTEMP(NOLOCK) WHERE SPID = @@SPID AND NUM = @PINUM)
  BEGIN
    SET @POMSG = '临时表没有数据，无法生成促销补差单'
    RETURN(1)
  END
  IF @PIBTYPE = 1
    SET @VNOTE = '由日结程序自动生成'
  IF @PIBTYPE = 2
    SET @VNOTE = '由单据 ' + @PIGENCLS + '，单号 ' + @PIGENNUM + ' 生成'
  SELECT @VSRC = USERGID FROM SYSTEM(NOLOCK)
  EXEC GENNEXTBILLNUMEX '', 'PRMOFFSET', @VNUM OUTPUT
  --不用照顾所有字段，只是抢个单号
  INSERT INTO PRMOFFSET(NUM, VDRGID, SETTLENO, FILDATE, FILLER, RECCNT, STAT,
    EON, LSTUPDTIME, OFFSETTYPE, OFFSETCALCTYPE, TOTAL, AMOUNT, TAX, BILLTO,
    GATHERINGMODE, SRC, SETTLEDEPTCODE, PSR, PAYDIRECT, BTYPE, NOTE)
  VALUES(@VNUM, @PIVENDOR, @PISETTLENO, GETDATE(), @PIOPERGID, 0, 0,
    1, GETDATE(), @PIOFFSETTYPE, @PIOFFSETCALCTYPE, 0, 0, 0, @PIVENDOR,
    @PIGATHERINGMODE, @VSRC, @PISETTLEDEPT, @PIPSR, @PIPAYDIRECT, @PIBTYPE, @VNOTE)
  --固化明细明细
  INSERT INTO PRMOFFSETDTLDTL(NUM, LINE, ITEM, GDGID, STOREGID, AGMNUM,
    AGMLINE, SAMT, RAMT, SQTY, RQTY, AGMTABLENAME)
  SELECT @VNUM, LINE, ITEM, GDGID, STOREGID, AGMNUM,
    AGMLINE, SAMT, RAMT, SQTY, RQTY, AGMTABLENAME
  FROM PRMOFFSETDTLDTLTEMP(NOLOCK)
  WHERE SPID = @@SPID AND NUM = @PINUM
  --固化明细
  INSERT INTO PRMOFFSETDTL(NUM, LINE, SETTLENO, GDGID, QPC, QPCSTR, AGMNUM,
  AGMLINE, SAMT, RAMT, OFFSETPRC, CNTINPRC, QTY, START, FINISH, NOTE, ALC,
  TOTAL, AMOUNT, TAX, DIFFPRC, PAYQTY, PAYAMT, SQTY, AGMTABLENAME)
  SELECT @VNUM, LINE, SETTLENO, GDGID, QPC, QPCSTR, AGMNUM,
  AGMLINE, SAMT, RAMT, OFFSETPRC, CNTINPRC, QTY, START, FINISH, NOTE, ALC,
  TOTAL, AMOUNT, TAX, DIFFPRC, PAYQTY, PAYAMT, SQTY, AGMTABLENAME
  FROM PRMOFFSETDTLTEMP(NOLOCK)
  WHERE SPID = @@SPID AND NUM = @PINUM
  --修改汇总的一些字段的值
  SELECT @VTOTAL = ISNULL(SUM(TOTAL), 0), @VAMOUNT = ISNULL(SUM(AMOUNT), 0), @VTAX = ISNULL(SUM(TAX), 0), @VRECCNT = COUNT(NUM)
  FROM PRMOFFSETDTL(NOLOCK)
  WHERE NUM = @VNUM
  UPDATE PRMOFFSET SET TOTAL = @VTOTAL, AMOUNT = @VAMOUNT, TAX = @VTAX, RECCNT = @VRECCNT
  WHERE NUM = @VNUM
  --传出补差单号
  SET @PONUM = @VNUM
  RETURN(0)
END
GO
