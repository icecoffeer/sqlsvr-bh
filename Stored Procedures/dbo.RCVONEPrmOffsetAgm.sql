SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[RCVONEPrmOffsetAgm]
(
  @SRC      INT,
  @ID       INT,
  @CLS      VARCHAR(10),
  @OPER     VARCHAR(30),
  @MSG      VARCHAR(255) OUTPUT
)
AS
BEGIN
  DECLARE
    @RET INT,
    @STORE INT,
    @NUM VARCHAR(14),
    @RCV INT,
    @FOUND CHAR(1),
    @cur_settleno int,
    @STAT INT,
    @NSTAT INT,
    @LAUNCH DATETIME
  select @cur_settleno = max(no) from monthsettle
  SET @RET = 0
  SELECT @STORE = USERGID FROM FASYSTEM(NOLOCK)
  SELECT @NUM = NUM, @RCV = RCV, @NSTAT = STAT
  FROM NPrmOffsetAgm(NOLOCK) WHERE SRC = @SRC AND ID = @ID
  IF @RCV <> @STORE
  BEGIN
    SET @MSG = '收到接收单位非本单位的促销补差协议；单号=' + @NUM
    EXEC NPrmOffsetAgm_REMOVE @SRC, @ID
    RETURN 1
  END

  EXEC @RET = RCVCHKPrmOffsetAgm @SRC, @ID, @CLS, @OPER, @MSG output
  IF @RET <> 0
  BEGIN
    SET @MSG = '接收促销补差协议' + @NUM + '失败。' + '单据存在不合法记录:' + @MSG;
    UPDATE NPrmOffsetAgm SET NNOTE = @MSG WHERE SRC = @SRC AND ID = @ID
    RETURN @RET
  END

  SELECT @STAT = STAT FROM PrmOffsetAgm(NOLOCK) WHERE NUM = @NUM
  IF @@ROWCOUNT = 1
    SET @FOUND = '1'
  ELSE
    SET @FOUND = '0'

  IF @FOUND = '1' AND @STAT IN(100, 800) AND @NSTAT IN(1400)
  BEGIN
    EXEC @RET = PRMOFFSETAGM_ABORT @NUM, @CLS, @NSTAT, @OPER, @MSG OUTPUT
    IF @RET <> 0
    BEGIN
      UPDATE NPRMOFFSETAGM SET NNOTE = @MSG WHERE SRC = @SRC AND ID = @ID
      RETURN @RET
    END
    EXEC NPRMOFFSETAGM_REMOVE @SRC, @ID
    RETURN 0
  END

  IF @FOUND = '1' OR (@FOUND = '0' AND @NSTAT IN(1400))
  BEGIN
    EXEC NPrmOffsetAgm_REMOVE @SRC, @ID
    RETURN 0
  END

  --插入到当前表中
  insert into PrmOffsetAgm (SRC, NUM, SETTLENO, STAT, VDRGID, BEGINDATE,
    ENDDATE, OFFSETVIEW, PSR, DeptLmt, LAUNCH, COVERALL, FILDATE, FILLER,
    CHKDATE, CHECKER, RECCNT, NOTE, LSTUPDTIME, SNDTIME, PRNTIME)
  select @src, NUM, SETTLENO, 0, VDRGID, BEGINDATE,
    ENDDATE, OFFSETVIEW, PSR, DeptLmt, LAUNCH, COVERALL, FILDATE, FILLER,
    CHKDATE, CHECKER, RECCNT, NOTE, LSTUPDTIME, SNDTIME, PRNTIME
  from NPrmOffsetAgm(NOLOCK)
  where SRC = @SRC AND ID = @ID

  insert into PrmOffsetAgmDTL(NUM, LINE, GDGID, MUNIT, QPC, QPCSTR,
    AGMCNTINPRC, AGMPRC, DIFFPRC, TOPQTY, TOPAMT, ASTART, AFINISH, NOTE)
  select N.NUM, N.LINE, G.LGID, N.MUNIT, N.QPC, N.QPCSTR,
    N.AGMCNTINPRC, N.AGMPRC, N.DIFFPRC, N.TOPQTY, N.TOPAMT, N.ASTART, N.AFINISH, N.NOTE
  from NPrmOffsetAgmDTL N(NOLOCK), GDXLATE G(NOLOCK)
  where G.NGID = N.GDGID
    and SRC = @SRC AND ID = @ID

  --门店接收时自动增加生效门店记录
  INSERT INTO PrmOffsetAgmLACSTORE(NUM, STOREGID)
    SELECT NUM, @STORE FROM NPrmOffsetAgm(NOLOCK)
    WHERE SRC = @SRC AND ID = @ID

  EXEC @RET = PRMOFFSETAGMCHK @NUM, @CLS, 100, '网络交换', @MSG output
  IF @RET <> 0
  BEGIN
    UPDATE NPrmOffsetAgm SET NNOTE = @MSG WHERE SRC = @SRC AND ID = @ID
    RETURN @RET
  END
  EXEC NPrmOffsetAgm_REMOVE @SRC, @ID
  RETURN 0
END
GO
