SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[GDINVCHGNOTIFY_RCV]
(
  @BILL_ID INT,
  @SRC_ID INT,
  @OPER CHAR(30),
  @MSG VARCHAR(255) OUTPUT
)
AS
BEGIN
  DECLARE
    @NET_NUM CHAR(14),
    @RCV_GID INT,
    @NET_STAT SMALLINT,
    @NET_TYPE SMALLINT,
    @RET INT

  SELECT @RCV_GID = RCV, @NET_STAT = STAT, @NET_TYPE = NTYPE, @NET_NUM = NUM
  FROM NGDINVCHGNOTIFY(NOLOCK) WHERE ID = @BILL_ID AND SRC = @SRC_ID
  IF @@ROWCOUNT = 0 OR @NET_NUM IS NULL
  BEGIN
    SET @MSG = '[接收]单据' +  @NET_NUM + '不存在'
    RETURN 1
  END

  IF EXISTS(SELECT 1 FROM GDINVCHGNOTIFY(NOLOCK) WHERE NUM = @NET_NUM AND STAT = @NET_STAT)
  BEGIN
    SET @MSG = '[接收]单据' +  @NET_NUM + '已经被接收'
    RETURN 2
  END

  IF (SELECT MAX(USERGID) FROM SYSTEM(NOLOCK)) <>  @RCV_GID
  BEGIN
    SET @MSG = '[接收]单据' +  @NET_NUM + '接收单位不是本单位'
    RETURN 3
  END

  IF @NET_TYPE <> 1
  BEGIN
    SET @MSG = '[接收]单据' +  @NET_NUM + '不在接收缓冲区中'
    RETURN 4
  END

  IF EXISTS(SELECT 1 FROM NGDINVCHGNOTIFYDTL(NOLOCK)
    WHERE SRC = @SRC_ID AND ID = @BILL_ID AND (OUTGDGID NOT IN (SELECT GID FROM GOODS(NOLOCK))
    OR INGDGID NOT IN (SELECT GID FROM GOODS(NOLOCK))))
  BEGIN
    SET @MSG = '[接收]单据' +  @NET_NUM + '商品资料在本地不存在'
    RETURN 5
  END

  DELETE FROM GDINVCHGNOTIFY WHERE NUM = @NET_NUM
  DELETE FROM GDINVCHGNOTIFYDTL WHERE NUM = @NET_NUM

  INSERT INTO GDINVCHGNOTIFY(NUM, SETTLENO, FILDATE, FILLER, CHECKER, CHKDATE, STAT, NOTE,
      RECCNT, LAUNCH, SNDTIME, PRNTIME, LSTUPDTIME, EON, SETPKG, SRC, EXDIRECT)
  SELECT NUM, SETTLENO, FILDATE, FILLER, CHECKER, CHKDATE, STAT, NOTE,
      RECCNT, LAUNCH, SNDTIME, PRNTIME, LSTUPDTIME, EON, SETPKG, SRC, EXDIRECT
  FROM NGDINVCHGNOTIFY(NOLOCK)
  WHERE SRC = @SRC_ID AND ID = @BILL_ID

  IF @@ERROR <> 0
  BEGIN
    SET @MSG = '[接收]接收' + @NET_NUM + '单据失败'
    RETURN 6
  END

  INSERT INTO GDINVCHGNOTIFYDTL(NUM, LINE, OUTGDGID, INGDGID, RELQTY, NOTE)
  SELECT NUM, LINE, OUTGDGID, INGDGID, RELQTY, NOTE
  FROM NGDINVCHGNOTIFYDTL(NOLOCK)
  WHERE SRC = @SRC_ID AND ID = @BILL_ID

  IF @@ERROR <> 0
  BEGIN
    SET @MSG = '[接收]接收' + @NET_NUM + '单据失败'
    RETURN 7
  END

  DELETE FROM NGDINVCHGNOTIFY WHERE ID = @BILL_ID AND SRC = @SRC_ID
  DELETE FROM NGDINVCHGNOTIFYDTL WHERE ID = @BILL_ID AND SRC = @SRC_ID
  
  IF @NET_STAT IN (100, 800) 
  BEGIN
    UPDATE GDINVCHGNOTIFY SET STAT = 0 WHERE NUM = @NET_NUM
    EXEC @RET = GDINVCHGNOTIFY_CHECK @NET_NUM, @OPER, '', 100, @MSG OUTPUT
    IF @RET <> 0 RETURN @RET
  END
  
  SET @MSG = '单据：' + @NET_NUM + '接收成功' + @MSG

  RETURN 0
END
GO
