SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[STKINBCKFIFOCHK]
	@PI_CLS CHAR(10),
	@PI_NUM CHAR(10),
	@PI_LINE INT,
	@PI_WRH INT,
	@PI_GDGID INT,
	@PI_QTY MONEY,
	@PI_PRICE MONEY OUTPUT,
	@PI_TOTAL MONEY OUTPUT,
	@PI_TAX MONEY OUTPUT,
	@PI_COST MONEY OUTPUT,
	@PI_ERRMSG VARCHAR(200) OUTPUT
AS
BEGIN
	DECLARE @AMT MONEY,@NEWSUBWRH INT
		,@SUBWRH INT,@OPTVALUE INT
		,@NEWTOTAL MONEY,@NEWPRC MONEY,@NEWTAX MONEY

	DECLARE @RETURN_STATUS INT

	IF @PI_QTY < 0
	BEGIN
		SELECT @PI_ERRMSG = '不允许为负数'
		RETURN 1005

	END

	EXEC CLEARTEMPSUBWRH @PI_GDGID,@PI_WRH

	IF EXISTS(SELECT 1 FROM STKINBCKDTL2 WHERE CLS= @PI_CLS AND NUM = @PI_NUM AND LINE = @PI_LINE)
	BEGIN
		INSERT INTO TEMPSUBWRH(SPID,WRH,SUBWRH,GDGID,QTY)
			SELECT @@SPID,WRH,SUBWRH,GDGID,QTY FROM STKINBCKDTL2
				WHERE CLS = @PI_CLS AND NUM = @PI_NUM AND LINE = @PI_LINE 
	END
	ELSE
	BEGIN
		--兼容使用的指定批次记录在STKINDTL中的情况
		SELECT @SUBWRH = 0
		SELECT @SUBWRH = ISNULL(SUBWRH,0) FROM STKINBCKDTL WHERE CLS = @PI_CLS AND NUM = @PI_NUM AND LINE = @PI_LINE 
		IF @SUBWRH <> 0
		BEGIN
			INSERT INTO TEMPSUBWRH(SPID,WRH,SUBWRH,GDGID,QTY)
				VALUES(@@SPID,@PI_WRH,@SUBWRH,@PI_GDGID,@PI_QTY)
		END
	END

	EXEC @RETURN_STATUS = UNLOADSUBWRH_2 @PI_GDGID,@PI_WRH,@PI_QTY,@PI_COST OUTPUT
	IF @RETURN_STATUS <> 0 RETURN @RETURN_STATUS
	
	DELETE FROM STKINBCKDTL2 WHERE CLS = @PI_CLS AND NUM = @PI_NUM AND LINE = @PI_LINE
	INSERT INTO STKINBCKDTL2(CLS,NUM,LINE,SUBWRH,WRH,GDGID,QTY,COST)
		SELECT @PI_CLS,@PI_NUM,@PI_LINE,SUBWRH,WRH,GDGID,QTY,COST FROM TEMPSUBWRH
			WHERE SPID = @@SPID AND GDGID = @PI_GDGID AND WRH = @PI_WRH

	SELECT @NEWSUBWRH= MIN(SUBWRH) FROM TEMPSUBWRH WHERE SPID = @@SPID AND WRH = @PI_WRH AND GDGID = @PI_GDGID

	IF @PI_CLS = '配货'
	BEGIN
		EXEC OPTREADINT 0, 'FrcAlcInBckPrc', 0, @OPTVALUE OUTPUT  
		IF @OPTVALUE = 1 
		BEGIN
			SELECT @NEWTOTAL = @PI_COST
			SELECT @NEWPRC = @NEWTOTAL / @PI_QTY
			SELECT @NEWTAX = @NEWTOTAL - (@NEWTOTAL)/(1 + TAXRATE/100) FROM GOODS WHERE GID = @PI_GDGID

			UPDATE STKINBCKDTL SET PRICE = @NEWPRC,TOTAL = @NEWTOTAL,TAX = @NEWTAX,
				SUBWRH = @NEWSUBWRH,COST = @PI_COST
				WHERE CLS = @PI_CLS AND NUM = @PI_NUM AND LINE = @PI_LINE

			UPDATE STKINBCK SET TOTAL = TOTAL + (@NEWTOTAL - @PI_TOTAL),TAX = TAX + (@NEWTAX - @PI_TAX)
				WHERE CLS = @PI_CLS AND NUM = @PI_NUM

			SELECT @PI_PRICE = @NEWPRC,@PI_TOTAL = @NEWTOTAL ,@PI_TAX = @NEWTAX
		END
		ELSE
		BEGIN
			UPDATE STKINBCKDTL SET COST = @PI_COST,SUBWRH = @NEWSUBWRH
				WHERE CLS = @PI_CLS AND NUM = @PI_NUM AND LINE = @PI_LINE
		END
	END
	ELSE
	BEGIN

		UPDATE STKINBCKDTL SET COST = @PI_COST,SUBWRH = @NEWSUBWRH
			WHERE CLS = @PI_CLS AND NUM = @PI_NUM AND LINE = @PI_LINE
	END
	EXEC CLEARTEMPSUBWRH @PI_GDGID,@PI_WRH

	RETURN 0

END
GO
