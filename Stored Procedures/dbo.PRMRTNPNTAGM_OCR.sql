SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[PRMRTNPNTAGM_OCR]
(
 @PICURTIME DATETIME,
 @PINUM VARCHAR(14), --协议号
 @PIOPERGID INT, --PRMOFFSET.FILLER 是整型
 @PIBTYPE SMALLINT, --0:人工录入；1：自动生成；2：其他单据
 @POPRMOFFSETNUM VARCHAR(14) OUTPUT, --促销补差单号
 @POMSG VARCHAR(255) OUTPUT
)
AS
BEGIN
  DECLARE
    @VBEGINTIME DATETIME, --协议开始时间
    @VENDTIME DATETIME, --协议结束时间
    @VSTAT SMALLINT, --协议状态
    @VRET SMALLINT, --返回值
    @VOPER VARCHAR(30), --操作人姓名代码
    @VLOGMSG VARCHAR(50), --记录日志信息
    @VRTNSTAT SMALLINT, --补差单生存状态。0：未生成；1：已审核。
    @VAUTOGEN SMALLINT,
    @OPT_PRMOFFSETSTAT SMALLINT --选项：生成的促销补差单的默认状态
  SELECT @VOPER = RTRIM(NAME) + '[' + RTRIM(CODE) + ']' FROM EMPLOYEE(NOLOCK) WHERE GID = @PIOPERGID
  IF @VOPER IS NULL SET @VOPER = '未知[-]'
  SELECT @VSTAT = STAT, @VRTNSTAT = RTNSTAT, @VBEGINTIME = BEGINTIME, @VENDTIME = ENDTIME, @VAUTOGEN = AUTOGEN
  FROM PRMRTNPNTAGM(NOLOCK)
  WHERE NUM = @PINUM
  IF @@ROWCOUNT = 0
  BEGIN
    SET @POMSG = '取协议失败'
    RETURN(1)
  END
  IF @VSTAT <> 100
  BEGIN
    SET @POMSG = '当前协议状态不是已审核，不能生成促销补差单'
    RETURN(1)
  END
  IF (@PICURTIME < @VENDTIME)
  BEGIN
    SET @POMSG = '协议尚未结束，无法生成促销补差单'
    RETURN(1)
  END
  IF @VRTNSTAT = 1
  BEGIN
    SET @POMSG = '促销补差单已审核，不能继续生成'
    RETURN(1)
  END
  IF (@PIBTYPE <> 1) AND (@VAUTOGEN = 1)
  BEGIN
    SET @POMSG = '生成模式为自动生成，不能通过其他方式生成促销补差单'
    RETURN(1)
  END
  --生成促销补差单
  EXEC @VRET = PRMRTNPNTAGM_GENPRMOFFSET @PINUM, @PIOPERGID, @PIBTYPE, @POPRMOFFSETNUM OUTPUT, @POMSG OUTPUT
  IF @VRET <> 0 RETURN @VRET
  --是否审核促销补差单
  EXEC OPTREADINT 8102, 'PRMOFFSETSTAT', 0, @OPT_PRMOFFSETSTAT OUTPUT
  IF @OPT_PRMOFFSETSTAT = 100
  BEGIN
    EXEC @VRET = PRMOFFSETCHK @POPRMOFFSETNUM, '', 100, @VOPER, @POMSG OUTPUT
    IF @VRET <> 0 RETURN @VRET
    --审核促销补差单后回写协议的返点状态为1
    UPDATE PRMRTNPNTAGM SET RTNSTAT = 1 WHERE NUM = @PINUM
  END
  SET @VLOGMSG = '生成促销补差单' + @POPRMOFFSETNUM
  --记录日志
  EXEC PRMRTNPNTAGM_ADD_LOG @PINUM, @VSTAT, @VLOGMSG, @VOPER
  RETURN(0)
END
GO
