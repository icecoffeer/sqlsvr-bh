SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[RCVONEPRMRTNPNTAGM]
(
  @SRC      INT,
  @ID       INT,
  @OPER     VARCHAR(30),
  @MSG      VARCHAR(255) OUTPUT
)
AS
BEGIN
  DECLARE
    @RET INT,
    @STORE INT,
    @NUM VARCHAR(14),
    @RCV INT,
    @FOUND CHAR(1),
    @CUR_SETTLENO int,
    @STAT INT,
    @NSTAT INT
  SELECT @CUR_SETTLENO = MAX(NO) FROM MONTHSETTLE(NOLOCK)
  SET @RET = 0
  SELECT @STORE = USERGID FROM FASYSTEM(NOLOCK)
  SELECT @NUM = NUM, @RCV = RCV, @NSTAT = STAT
  FROM NPRMRTNPNTAGM(NOLOCK) WHERE SRC = @SRC AND ID = @ID
  IF @RCV <> @STORE
  BEGIN
    SET @MSG = '收到接收单位非本单位的促销返点协议。协议号=' + @NUM
    EXEC NPRMRTNPNTAGM_REMOVE @SRC, @ID
    RETURN 0
  END

  SELECT @STAT = STAT FROM PRMRTNPNTAGM(NOLOCK) WHERE NUM = @NUM
  IF @@ROWCOUNT = 1
    SET @FOUND = '1'
  ELSE
    SET @FOUND = '0'

  IF @FOUND = '1' AND @STAT = 100 AND @NSTAT IN (300, 1400) --唯二正确可处理状态
  BEGIN
    EXEC @RET = PRMRTNPNTAGM_ON_MODIFY @NUM = @NUM, @TOSTAT = @NSTAT, @OPER = '网络交换', @MSG = @MSG OUTPUT
    IF @RET <> 0
    BEGIN
      UPDATE NPRMRTNPNTAGM SET NNOTE = @MSG WHERE SRC = @SRC AND ID = @ID
      RETURN @RET
    END
    EXEC NPRMRTNPNTAGM_REMOVE @SRC, @ID
    RETURN 0
  END

  IF @FOUND = '1' OR (@FOUND = '0' AND @NSTAT = 1400)
  BEGIN
    EXEC NPRMRTNPNTAGM_REMOVE @SRC, @ID
    RETURN 0
  END

  --插入到当前表中
  INSERT INTO PRMRTNPNTAGM (NUM, SETTLENO, STAT, RECCNT, FILDATE, FILLER, VENDOR,
    BEGINTIME, ENDTIME, AUTOGEN, GENTIME, RTNSTAT, LSTUPDTIME, LSTUPDOPER, CHKTIME,
    CHECKER, SNDTIME, PRNTIME, NOTE)
  SELECT NUM, @CUR_SETTLENO, 0, RECCNT, FILDATE, FILLER, VENDOR,
    BEGINTIME, ENDTIME, AUTOGEN, GENTIME, RTNSTAT, LSTUPDTIME, LSTUPDOPER, CHKTIME,
    CHECKER, SNDTIME, PRNTIME, NOTE
  FROM NPRMRTNPNTAGM(NOLOCK)
  WHERE SRC = @SRC AND ID = @ID

  INSERT INTO PRMRTNPNTAGMDTL(NUM, LINE, DEPT, BRAND, NOTE)
  SELECT NUM, LINE, DEPT, BRAND, NOTE
  FROM NPRMRTNPNTAGMDTL(NOLOCK)
  WHERE SRC = @SRC AND ID = @ID
  
  INSERT INTO PRMRTNPNTAGMEXGDDTL(NUM, LINE, GDGID)
  SELECT NUM, LINE, GDGID
  FROM NPRMRTNPNTAGMEXGDDTL(NOLOCK)
  WHERE SRC = @SRC AND ID = @ID
  
  INSERT INTO PRMRTNPNTAGMRATEDTL(NUM, LINE, LOWAMT, HIGHAMT, RATE)
  SELECT NUM, LINE, LOWAMT, HIGHAMT, RATE
  FROM NPRMRTNPNTAGMRATEDTL(NOLOCK)
  WHERE SRC = @SRC AND ID = @ID

  --门店接收时自动增加生效门店记录
  INSERT INTO PRMRTNPNTAGMLACSTORE(NUM, STOREGID)
  SELECT NUM, @STORE FROM NPRMRTNPNTAGM(NOLOCK)
  WHERE SRC = @SRC AND ID = @ID

  EXEC @RET = PRMRTNPNTAGM_MODIFYTO100 @NUM, 100, '网络交换', @MSG OUTPUT
  IF @RET <> 0
  BEGIN
    UPDATE NPRMRTNPNTAGM SET NNOTE = @MSG WHERE SRC = @SRC AND ID = @ID
    RETURN @RET
  END
  EXEC NPRMRTNPNTAGM_REMOVE @SRC, @ID
  EXEC PRMRTNPNTAGM_ADD_LOG @NUM, 100, '接收网络单据', @OPER;
  RETURN 0
END
GO
